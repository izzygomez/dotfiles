#!/bin/zsh

# Sourced by ~/.zshrc. Not intended to be run standalone; may rely on
# variables & functions defined earlier in the sourcing chain.

source $dotfiles_dir/zsh/checkers.sh

# Wrapper around Graphite's `gt` command. Does a few things:
# - Checks if git-crypt files are unlocked. Because `graphite/user_config` is
#   one of those files, we limit the use of `gt` to only when the files are
#   unlocked.
# - Intercepts `gt submit` to also print GitHub PR links. Graphite only shows
#   its own dashboard links in the output; this extracts owner/repo/number &
#   prints the corresponding GitHub URL(s).
# - All other `gt` subcommands pass through unchanged.
gt() {
    if ! are_git_crypt_files_unlocked; then
        echo "zshrc_gt: git-crypt files are locked, can't use gt."
        return 1
    fi

    if [[ $1 != "submit" ]]; then
        command gt "$@"
        return $?
    fi
    shift # remove "submit" from args

    local tmpfile
    tmpfile=$(mktemp)
    local exit_code

    # Use `script` to capture output while preserving the TTY so that
    # interactive prompts (title, body, confirm, etc.) still work.
    if is_mac_os; then
        script -q "$tmpfile" command gt submit "$@"
        exit_code=$?
    elif is_linux; then
        script -q -c "command gt submit $*" "$tmpfile"
        exit_code=$?
    else
        command gt submit "$@"
        return $?
    fi

    # Strip ANSI escape codes & carriage returns that `script` leaves
    # behind, then convert any Graphite PR URLs to GitHub PR URLs.
    local github_urls
    github_urls=$(
        sed $'s/\x1b\[[0-9;]*[a-zA-Z]//g; s/\r//g' "$tmpfile" |
            grep -oE 'https://app\.graphite\.com/github/pr/[^[:space:]]+' |
            sed 's|app\.graphite\.com/github/pr/\([^/]*\)/\([^/]*\)/\([0-9]*\)|github.com/\1/\2/pull/\3|'
    )

    if [[ -n $github_urls ]]; then
        echo ""
        echo "$github_urls" | while IFS= read -r url; do
            echo "GitHub PR: $url"
        done
    fi

    rm -f "$tmpfile"
    return $exit_code
}

# Added as part of setting up the gt command line tool
#compdef gt
###-begin-gt-completions-###
#
# yargs command completion script
#
# Installation: gt completion >> ~/.zshrc
#    or gt completion >> ~/.zprofile on OSX.
#
_gt_yargs_completions() {
    local reply
    local si=$IFS
    IFS=$'
' reply=($(COMP_CWORD="$((CURRENT - 1))" COMP_LINE="$BUFFER" COMP_POINT="$CURSOR" gt --get-yargs-completions "${words[@]}"))
    IFS=$si
    _describe 'values' reply
}
compdef _gt_yargs_completions gt
###-end-gt-completions-###
